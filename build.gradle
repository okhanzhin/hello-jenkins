plugins {
    id 'java'
    id 'jacoco'
    id("com.diffplug.spotless") version "6.25.0"
}

group = 'com.okhanzhin'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'
}

jacoco {
    toolVersion = "0.8.10"
}

test {
    useJUnit()
    finalizedBy jacocoTestReport
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.17.0')
                .reflowLongStrings()
                .formatJavadoc(false)
                .reorderImports(false)
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = ['**/App.class']

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50  // Минимум 50% покрытия
            }
        }
    }
}

jar {
    archiveFileName = "hello-jenkins-${version}.jar"
    manifest {
        attributes 'Main-Class': 'com.okhanzhin.hellojenkins.App'
    }
}

tasks.register('printArtifacts') {
    doLast {
        println "==========================================="
        println "BUILD ARTIFACTS"
        println "==========================================="
        println "JAR файл: ${buildDir}/libs/${jar.archiveFileName.get()}"
        println "JaCoCo Report: ${buildDir}/reports/jacoco/test/html/index.html"
        println "==========================================="
    }
}

tasks.named("check") {
    dependsOn("spotlessApply")
}